group 'io.github.thisisnozaku.charactercreator'
version '1.0'

buildscript {
    dependencies {
        classpath 'be.jlr-home.gradle:scrPlugin:0.1.+'
    }
    repositories {
        mavenLocal()
        mavenCentral()
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'scr'
apply plugin: 'osgi'

sourceCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    compile "io.github.thisisnozaku.charactercreator:neone-plugin-api:1.0"
    compile 'org.apache.felix:org.apache.felix.scr.annotations:1.9.12'
    compile 'com.jayway.jsonpath:json-path:2.2.0'
    compile "javax.json:javax.json-api:1.0"

    runtime "org.glassfish:javax.json:1.0.4"
}

jar {
    manifest {
        instruction 'Bundle-Category', 'plugin'
        instruction 'Bundle-Description', 'Only War Plugin.'
        instructionReplace 'Private-Package', 'io.github.thisisnozaku.*'
        instructionReplace 'Export-Package', '!*'
        instruction 'Import-Package', 'io.github.thisisnozaku.charactercreator.plugins; version=1.0.0'
    }
    from sourceSets.main.allSource
    from {
        configurations.runtime.collect {it.isDirectory() ? it : zipTree(it)}
    }
}

sourceSets {
    main {
        output.resourcesDir "$buildDir/resources"
        resources {
            srcDirs = ['src/main/resources/pluginresource']
        }
    }
}

processResources {
    outputs.upToDateWhen {
        file("$buildDir/resources/templates/character.html").exists()
    }
    exclude "**/*.ts"
    exclude "**/*.js.map"
    exclude "**/tests/**"
}

/*
* Move character.html, which is kept in the root of the resources directory
* for ease of testing due to relative urls, into the resources/templates
* build directory.
 */
processResources << {
    String source = "src/main/resources/character.html"
    String dest = "$buildDir/resources/templates/"
    copy {
        from source
        into dest
    }
}
